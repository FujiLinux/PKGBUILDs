# Maintainer: Evan Welsh <contact@evanwelsh.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=fujilinux-keyring
_tag='8fe455e27e35a55a15d714637024b69f9dceab89' # git rev-parse ${pkgver}
pkgver=20240317
pkgrel=1
pkgdesc='Fuji Linux PGP keyring'
arch=('any')
url='https://github.com/FujiLinux/fujilinux-keyring'
license=('GPL-3.0-or-later')
install=$pkgname.install
depends=('pacman')
makedepends=('git' 'python' 'sequoia-sq' 'pkgconf' 'systemd')
checkdepends=('python-coverage' 'python-pytest')
source=("fujilinux-keyring::git+https://github.com/FujiLinux/fujilinux-keyring.git#tag=${_tag}?signed")
sha256sums=('SKIP')
validpgpkeys=('FB398BBE527AFF9D7F4C34C9069381A0EC5A60D0')  # Evan Welsh <ewlsh@fujilinux.org>

build() {
  cd fujilinux-keyring/

  make build
}

check() {
  cd fujilinux-keyring/

  make check
}

package() {
  cd fujilinux-keyring/

  make PREFIX='/usr' DESTDIR="${pkgdir}" install
}
